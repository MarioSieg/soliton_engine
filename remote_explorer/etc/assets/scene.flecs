// Script that creates a house template
// and populates a small town with it.
//
// For C/C++ examples and docs go to:
//  https://github.com/SanderMertens/flecs

using flecs.components.*
using flecs.game
const PI = 3.1415926

materials {
  prefab Stone {
    Rgb: {0.4, 0.4, 0.4}
  }

  prefab DarkStone {
    Rgb: {0.1, 0.1, 0.1}
  }

  prefab Wood {
    Rgb: {0.13, 0.08, 0.03}
  }

  prefab Metal {
    Rgb: {0.02, 0.02, 0.02}
    Specular: {0.01, 5.0}
  }

  prefab Light {
    Rgb: {0.7, 0.5, 0.15}
    Emissive: {3}
    PointLight: {
      color: [0.7, 0.5, 0.15]
      distance: 0.5
      intensity: 0.5
    }
  }

  prefab Smoke {
    Rgb: {0.3, 0.3, 0.3}
    auto_override | Box: {0.15, 0.15, 0.15}
    Velocity3: {0.1, 0.5, 0}
    Emissive: {0.7}
  }
}

template Door {
  prop width = f32: 0.75
  prop height = f32: 1.5
  
  (IsA, materials.Wood)
  Box: {$width, $height, 0.2}

  with Box(0.5, 0.1, 0.3) {
    Hinge1 : materials.Metal {
      Position3: {-$width / 2 + 0.2, $height / 2 - 0.3, 0}
    }
    Hinge2 : materials.Metal {
      Position3: {-$width / 2 + 0.2, -$height / 2 + 0.3, 0}
    }
  }
}

template Lantern {
  Mount : materials.Wood {
    Position3: {0, 0, 0}
    Box: {0.2, 0.2, 0.2}
  }

  Hanger : materials.Wood {
    Position3: {0, 0, -0.25}
    Box: {0.1, 0.1, 0.5}
  }

  LanternLight : materials.Light {
    Position3: {0, -0.1, -0.45}
    Box: {0.2, 0.2, 0.2}
  }
}

template Chimney {
  prop height = f32: 3.0

  (IsA, materials.DarkStone)
  Box: {0.4, $height, 0.4}

  smoke {
    Position3: {0.0, $height / 2, 0}
    // Volume from which particles are emitted
    Box: {0.2, 0.2, 0.2}
    ParticleEmitter: {
      particle: materials.Smoke
      spawn_interval: 0.05
      size_decay: 0.5
    }
  }
}

template House {
  prop width = f32: 3
  prop height = f32: 3
  
  Walls : materials.Stone {
    Position3: {0, $height / 2, 0}
    Box: {$width, $height, $width}
  }

  Floor : materials.DarkStone {
    Position3: {0, 0, 0}
    Box: {$width + 0.3, 0.2, $width + 0.3}
  }

  Attic : materials.Wood {
    const w_half_pow = 0.5 * ($width * $width)
    const attic_height = $w_half_pow.sqrt()
    Position3: {0, $height, 0}
    Rotation3: {0, 0, $PI / 4}
    Box: {$attic_height, $attic_height, $width * 0.9}
  }

  Roof {
    const overshoot = 0.5
    const w_half = ($overshoot + $width) / 2
    const w_quart = ($overshoot + $width) / 4
    const w_roof_sqr = $w_half * $w_half + $w_half * $w_half
    const roof_length = $w_roof_sqr.sqrt()

    Left : materials.Wood {
      Position3: {-$w_quart, $w_quart + $height - 0.5 * $overshoot, 0}
      Rotation3: {0, 0, $PI / 4}
      Box: {$roof_length, 0.25, $width + $overshoot}
    }

    Right : materials.Wood {
      Position3: {$w_quart, $w_quart + $height - 0.5 * $overshoot, 0}
      Rotation3: {0, 0, -$PI / 4}
      Box: {$roof_length, 0.25, $width + $overshoot}
    }
  }

  // Vertical bars at the corner of the house
  with (IsA, materials.Wood) {
    with Box(0.2, $height, 0.2) {
      Position3 (-$width / 2, $height / 2, -$width / 2)
      Position3 ($width / 2, $height / 2, -$width / 2)
      Position3 ($width / 2, $height / 2, $width / 2)
      Position3 (-$width / 2, $height / 2, $width / 2)
    }

    // Horizontal bars at the top
    with Box(0.2, $width, 0.2) {
      with Rotation3(0, 0, $PI / 2) {
        Position3 (0, $height, -$width / 2)
        Position3 (0, $height, $width / 2)

        // If it's a tall house, add more bars
        if $height > 3.5 {
          Position3 (0, 3.0, -$width / 2)
          Position3 (0, 3.0, $width / 2)
        }
      }
    }
  }

  Door()    { Position3: {0,   0.75, -$width / 2} }
  Lantern() { Position3: {0.75, 1.5, -$width / 2} }
  Chimney(height: $height + 1.5) { 
    Position3: {1.0, ($height + 1.5) / 2, 0}
  }
}

town {
  House(height: 2.5)
  House(height: 3.0) { Position3: {-5.0, 0, -2.0};  Rotation3: {y: -0.7} }
  House(height: 2.0) { Position3: { 5.0, 0, -2.5};  Rotation3: {y: 1.0} }

  House(height: 3.5) { Position3: {-10.0, 0, 5.0};  Rotation3: {y: -0.1} }
  House(height: 4.0) { Position3: {-5.0,  0, 5.0};  Rotation3: {y: -0.7} }
  House(height: 5.0) { Position3: { 0.0,  0, 7.0};  Rotation3: {y: 0.9} }
  House(height: 3.5) { Position3: { 5.0,  0, 5.0};  Rotation3: {y: -0.4} }
  House(height: 3.0) { Position3: { 10.0, 0, 2.0};  Rotation3: {y: 2.2} }

  House(height: 6.0) { Position3: {-10.0, 0, 13.0}; Rotation3: {y: -0.7} }
  House(height: 6.5) { Position3: {-5.0,  0, 12.0}; Rotation3: {y: 0.8} }
  House(height: 8.0) { Position3: { 0.0,  0, 13.0}; Rotation3: {y: 1.0} }
  House(height: 7.0) { Position3: { 5.0,  0, 11.0}; Rotation3: {y: -0.3} }
  House(height: 6.5) { Position3: { 11.0, 0, 11.0}; Rotation3: {y: 0.7} }
  House(height: 5.5) { Position3: { 15.0, 0, 7.0};  Rotation3: {y: 0.5} }
}
